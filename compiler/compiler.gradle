apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'

File antlrDefDir = file('src/antlr/def')
File antlrGenDir = file('src/antlr/gen')

// ----------------------------------------------------------------------------------------------------
//    ____                                    ____         _
//   / ___|   ___   _   _  _ __  ___  ___    / ___|   ___ | |_  ___
//   \___ \  / _ \ | | | || '__|/ __|/ _ \   \___ \  / _ \| __|/ __|
//    ___) || (_) || |_| || |  | (__|  __/    ___) ||  __/| |_ \__ \
//   |____/  \___/  \__,_||_|   \___|\___|   |____/  \___| \__||___/
//
// ----------------------------------------------------------------------------------------------------

sourceSets {
  antlr {
    java { srcDirs = [antlrGenDir]}
    resources { srcDirs = [antlrDefDir]}
  }
}

// ----------------------------------------------------------------------------------------------------
//    ____                                 _                     _
//   |  _ \   ___  _ __    ___  _ __    __| |  ___  _ __    ___ (_)  ___  ___
//   | | | | / _ \| '_ \  / _ \| '_ \  / _` | / _ \| '_ \  / __|| | / _ \/ __|
//   | |_| ||  __/| |_) ||  __/| | | || (_| ||  __/| | | || (__ | ||  __/\__ \
//   |____/  \___|| .__/  \___||_| |_| \__,_| \___||_| |_| \___||_| \___||___/
//                |_|
// ----------------------------------------------------------------------------------------------------

configurations {
  antlr4
  compile.extendsFrom annotationProccessing, antlrCompile
}

dependencies {
  antlr4 libs.antlr4

  antlrCompile libs.antlr4_runtime

  compile sourceSets.antlr.output
  compile libs.guava
  compile libs.jsr305
  compile libs.lombok
  compile libs.nbt

  annotationProccessing libs.pojobuilder

  testCompile libs.assertj
  testCompile libs.guava
  testCompile libs.junit
  testCompile libs.spock
}

// ----------------------------------------------------------------------------------------------------
//       _            _    _
//      / \    _ __  | |_ | | _ __
//     / _ \  | '_ \ | __|| || '__|
//    / ___ \ | | | || |_ | || |
//   /_/   \_\|_| |_| \__||_||_|
//
// ----------------------------------------------------------------------------------------------------

task cleanAntlr4 << {
  delete antlrGenDir
  mkdir antlrGenDir
}

task generateAntlr4 {
  description = 'Generates Java sources from ANTLR4 grammars.'
  logging.captureStandardOutput LogLevel.QUIET

  fileTree(dir: antlrDefDir, include:'**/*.g4').each { File file ->
    doLast {
      String relativePathToGrammar = antlrDefDir.toURI().relativize( file.getParentFile().toURI() ).toString()
      File grammarDestDir = new File(antlrGenDir, relativePathToGrammar)
      mkdir grammarDestDir
      String packageName = relativePathToGrammar.replaceFirst('/$', '').replace('/', '.')
      println "  Generating grammar sources for ${file.name} in package $packageName"
      javaexec {
        classpath configurations.antlr4
        main = 'org.antlr.v4.Tool'
        args = ["-o", grammarDestDir, "-package", packageName, "-visitor", // ggf. Konfig auch fÃ¼r Listener (suche Listener)
          "${file.canonicalPath}"]
      }
    }
  }
}
generateAntlr4.dependsOn cleanAntlr4
compileJava.dependsOn generateAntlr4
eclipseClasspath.dependsOn generateAntlr4

// ----------------------------------------------------------------------------------------------------
//        _
//       | |  __ _  _ __
//    _  | | / _` || '__|
//   | |_| || (_| || |
//    \___/  \__,_||_|
//
// ----------------------------------------------------------------------------------------------------

jar {
  from sourceSets.antlr.output.classesDir
  manifest {
    attributes(
        'Created-By':               'Adrodoc55 (adrodoc55@googlemail.com)',
        'Specification-Title':      productName,
        'Specification-Version':    version,
        'Implementation-Title':     productName,
        'Implementation-Version':   version,
        )
  }
}


// ----------------------------------------------------------------------------------------------------
//    _____       _  _
//   | ____| ___ | |(_) _ __   ___   ___
//   |  _|  / __|| || || '_ \ / __| / _ \
//   | |___| (__ | || || |_) |\__ \|  __/
//   |_____|\___||_||_|| .__/ |___/ \___|
//                     |_|
// ----------------------------------------------------------------------------------------------------

eclipse {
  jdt {
    file {
      withProperties { properties ->
        properties << ['org.eclipse.jdt.core.compiler.processAnnotations':'enabled']
      }
    }
  }
  classpath {
    file {
      whenMerged { cp ->
        def folder = cp.entries.find { it.path == 'src/antlr/gen' }
        folder.entryAttributes << ['ignore_optional_problems': true]
      }
    }
  }
}

task eclipseFactorypath {
  ext.factorypath = new File('.factorypath')
} << {
  def writer = new StringWriter()
  def xml = new groovy.xml.MarkupBuilder(writer)
  xml.factorypath {
    configurations.annotationProccessing.each {
      factorypathentry kind: 'EXTJAR', id: it, enabled: "true", runInBatchMode: "false"
    }
  }
  factorypath.text = writer.toString()
}

task cleanEclipseFactorpath(type: Delete) { delete eclipseFactorypath.factorypath }

task eclipseApt {
  File eclipseSettings = file('.settings')
  eclipseSettings.mkdirs()
  ext.jdtAptCorePrefs = new File(eclipseSettings, 'org.eclipse.jdt.apt.core.prefs')
} << { jdtAptCorePrefs.text = """\
eclipse.preferences.version=1
org.eclipse.jdt.apt.aptEnabled=true
org.eclipse.jdt.apt.genSrcDir=.apt_generated
org.eclipse.jdt.apt.reconcileEnabled=true
""" }

task cleanEclipseApt(type: Delete) { delete eclipseApt.jdtAptCorePrefs }

tasks.cleanEclipse.dependsOn cleanEclipseFactorypath
tasks.cleanEclipse.dependsOn cleanEclipseApt
tasks.eclipse.dependsOn eclipseFactorypath
tasks.eclipse.dependsOn eclipseApt
