apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'antlr'

task wrapper (type: Wrapper) {
	gradleVersion = 2.9
}

repositories {
	mavenCentral()
}

configurations {
	compile.extendsFrom annotationProccessing
}

dependencies {
    antlr 'org.antlr:antlr:3.5.2'
    
	annotationProccessing 'net.karneim:pojobuilder:3.4.0'

	testCompile 'org.assertj:assertj-core:3.2.0'
	testCompile 'com.google.guava:guava:18.0'
	testCompile 'junit:junit:4.12'
}

String srcGenAntlrDir = 'src-gen/main/java'

sourceSets {
	main {
		java {
			srcDir srcGenAntlrDir
		}
	}
}

task cleanGeneratedSources << {
	delete file(srcGenAntlrDir)
}

generateGrammarSource {
	dependsOn cleanGeneratedSources
	outputDirectory = new File(file(srcGenAntlrDir), 'de/adrodoc55/antlr/mpl')
}

jar {
    manifest {
        attributes(
            'Main-Class':   'de.adrodoc55.minecraft.mpl.Main'
            )
    }
}





eclipse {
	jdt {
		file {
			withProperties { properties ->
				properties << ['org.eclipse.jdt.core.compiler.processAnnotations':'enabled']
			}
		}
	}
}

task eclipseFactorypath {
	ext.factorypath = new File('.factorypath')
} << {
	def writer = new StringWriter()
	def xml = new groovy.xml.MarkupBuilder(writer)
	xml.factorypath {
		configurations.annotationProccessing.each {
			factorypathentry kind: 'EXTJAR', id: it, enabled: "true", runInBatchMode: "false"
		}
	}
	factorypath.text = writer.toString()
}

task cleanEclipseFactorpath(type: Delete) {
	delete eclipseFactorypath.factorypath
}

task eclipseApt {
	File eclipseSettings = file('.settings')
	eclipseSettings.mkdirs()
	ext.jdtAptCorePrefs = new File(eclipseSettings, 'org.eclipse.jdt.apt.core.prefs')
} << {
	jdtAptCorePrefs.text = """\
eclipse.preferences.version=1
org.eclipse.jdt.apt.aptEnabled=true
org.eclipse.jdt.apt.genSrcDir=.apt_generated
org.eclipse.jdt.apt.reconcileEnabled=true
"""
}

task cleanEclipseApt(type: Delete) {
	delete eclipseApt.jdtAptCorePrefs
}

tasks.cleanEclipse.dependsOn cleanEclipseFactorypath
tasks.cleanEclipse.dependsOn cleanEclipseApt
tasks.eclipse.dependsOn cleanEclipse
tasks.eclipse.dependsOn eclipseFactorypath
tasks.eclipse.dependsOn eclipseApt
